buildscript {
    dependencies {
        classpath "org.akhikhl.gretty:gretty:1.2.4"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
 }

apply plugin: 'war'
apply plugin: "org.akhikhl.gretty"

dependencies {
	configurations.all {
		exclude group: 'amazon', module: 'aws-java-sdk'
		exclude group: 'com.thoughtworks.xstream', module: 'xstream'
		exclude group: 'io.netty', module: 'netty-resolver-dns'
		exclude group: 'com.squareup.okhttp', module: 'okhttp'
		resolutionStrategy {
	       	force 'net.java.dev.jna:jna:+'
			force "com.fasterxml.jackson.core:jackson-core:${revJacksonCore}"
			force "com.fasterxml.jackson.core:jackson-databind:${revJacksonDatabind}"
			force "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${revJaxrsJackson}"
			force 'commons-beanutils:commons-beanutils:+'
			force 'commons-collections:commons-collections:+'
			force 'io.netty:netty-common:4.1.48.Final'
			force 'io.netty:netty-transport:4.1.48.Final'
			force 'io.netty:netty-codec:4.1.48.Final'
			force 'io.netty:netty-codec-http:4.1.53.Final'
			force 'io.netty:netty-codec-http2:4.1.48.Final'
			force 'xerces:xercesImpl:2.12.0'
			force 'org.apache.httpcomponents:httpclient-cache:+'
			force 'net.java.dev.jna:jna-platform:+'
			force 'com.amazonaws:aws-java-sdk-sqs:+'
			force "org.apache.thrift:libthrift:+"
			force "net.minidev:json-smart:+"
			force "org.apache.logging.log4j:log4j-api:2.17.0"
    		force "org.apache.logging.log4j:log4j-core:2.17.0"
		}
	}                                                                                       

    //Conductor
    compile project(':conductor-core')
    compile project(':conductor-jersey')
    compile project(':conductor-redis-persistence')
    compile project(':conductor-mysql-persistence')
    compile project(':conductor-postgres-persistence')
    compile project(':conductor-cassandra-persistence')
    compile project(':conductor-contribs')
    compile project(':conductor-jwt-security')
    compile project(':conductor-es7-persistence')
    compile project(':conductor-fb-es7-persistence')
    compile project(':conductor-grpc-server')
    compile project(':conductor-zookeeper-lock')
    compile project(':conductor-redis-lock')

    compile "com.netflix.runtime:health-guice:${revHealth}"

    //Jetty
    compile "org.eclipse.jetty:jetty-server:${revJetteyServer}"
    compile "org.eclipse.jetty:jetty-servlet:${revJettyServlet}"
    compile "org.eclipse.jetty:jetty-jmx:${revJettyServlet}"

    //Guice
    compile "com.sun.jersey.contribs:jersey-guice:${revJerseyGuice}"
    compile "com.google.inject:guice:${revGuice}"
    compile "com.google.inject.extensions:guice-servlet:${revGuiceServlet}"

    //Swagger
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.12.0-rc1"
    compile "io.swagger:swagger-jersey-jaxrs:${revSwagger}"
}

shadowJar {
    zip64 true
    mergeServiceFiles()
    configurations = [project.configurations.compile]
    manifest {
        attributes 'Description': 'Self contained Conductor server jar'
        attributes 'Main-Class': 'com.netflix.conductor.bootstrap.Main'
    }
}
publishing {
    publications {
        nebula(MavenPublication) {
            artifact shadowJar
        }
    }
}

gretty {
    contextPath = '/'
    servletContainer = 'tomcat8'
    scanDirs = ['**/src/main/resources/**']
    scanDependencies = true
    port = 8080
    // More properties can be found here:
    // http://akhikhl.github.io/gretty-doc/Gretty-configuration.html
}
configurations.grettyRunnerTomcat8 {
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}


build.dependsOn('shadowJar')

task server(type: JavaExec) {
    systemProperty 'workflow.elasticsearch.url', 'localhost:9300'
    // Switch between Elasticsearch versions 2 & 5 with major version number.
    systemProperty 'loadSample', 'true'
    // Unlink Jetty logger to reduce clutter in local server.
    systemProperty 'org.eclipse.jetty.util.log.class', 'org.eclipse.jetty.util.log.StdErrLog'
    // Set Jetty logger level to INFO.
    systemProperty 'org.eclipse.jetty.LEVEL', 'INFO'
    systemProperties System.properties
    main = 'com.netflix.conductor.bootstrap.Main'
    args  'src/main/resources/server.properties', 'src/main/resources/log4j.properties'
    classpath = sourceSets.test.runtimeClasspath
}
